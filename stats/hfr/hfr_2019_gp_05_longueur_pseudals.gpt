# nettoyage des données

#sed -i "s/\"//g" hfr_2019_dt_longueur_pseudals.txt
#sed -i "s/,/ /g" hfr_2019_dt_longueur_pseudals.txt
#sed -i "1d" hfr_2019_dt_longueur_pseudals.txt
#del sed*


# le graph

unset multiplot
reset session
reset
set encoding utf8
set terminal svg size 1600,900 dynamic name "hfr_2019_02_longueur_pseudals_svg" font "Verdana,Arial,Sans-serif,Helvetica,14" background "#ffffff"
set output "hfr_2019_02_longueur_pseudals.svg"
set mytics 2
set key at 43.5,180000 center right Left opaque reverse samplen 3 box spacing 1.3 width -10 height .2
set grid xtics ytics mytics back lc "#3f3f3f" dt (10,10), lc "#7f7f7f" dt (10,10)
set title "{/:Bold=18 hfr : la longueur des pseudals\n{/=12 (données collectées entre le 5 avril 2019 à 19:42:51 et le 12 avril 2019 à 2:48:34)"
set xlabel "{/=16 la longeur du pseudal" offset 0,0.4
set ylabel "{/=16 les comptes hfr"
set xtics 0, 1, 44 offset 0,0.3
set ytics 20000
set style fill transparent solid 0.4
set key tc variable

set arrow 1 from    8,0 to    8,200000 nohead front lw 2 lc "#ff007f" dt (10,7)
set arrow 2 from 8.23,0 to 8.23,200000 nohead front lw 2 lc "#7f00ff" dt (10,7)

set label 1 "{/:Bold 8"    at    8,195500 right front tc rgbcolor "#ff007f" offset -.6,0
set label 2 "{/:Bold 8.23" at 8.23,195500 left  front tc rgbcolor "#7f00ff" offset  .6,0

plot [-1:45][0:200000] "hfr_2019_dt_longueur_pseudals.txt" w boxes lw 2 lc rgbcolor "#003fbf" title "{/:Bold le nombre de comptes hfr en fonction de la longueur du pseudal", \
keyentry w lines lw 2 lc rgbcolor "#ff007f" dt (10,7) title "{/:Bold longueur médiane", \
keyentry w lines lw 2 lc rgbcolor "#7f00ff" dt (10,7) title "{/:Bold longueur moyenne"

set output


