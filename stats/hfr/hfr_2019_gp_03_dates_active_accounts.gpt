# nettoyage des données

#sed -i "s/\"//g" hfr_2019_dt_dates_active_accounts.txt
#sed -i "s/,/ /g" hfr_2019_dt_dates_active_accounts.txt
#sed -i "1d" hfr_2019_dt_dates_active_accounts.txt
#del sed*


# le graph

unset multiplot
reset session
reset
set encoding utf8
set terminal svg size 1600,900 dynamic name "hfr_2019_12_dates_actives_accounts_svg" font "Verdana,Arial,Sans-serif,Helvetica,14" background "#ffffff"
set output "hfr_2019_12_dates_active_accounts.svg"
set multiplot title "{/:Bold=18 hfr : les comptes hfr actifs\n{/=12 (données collectées entre le 5 avril 2019 à 19:42:51 et le 12 avril 2019 à 2:48:34)"
set style fill transparent solid 0.4
set key tc variable
set grid xtics ytics mxtics mytics back lc "#3f3f3f" dt (10,10), lc "#7f7f7f" dt (10,10)
set xtics 1
set format y "%7.f"
set xlabel "{/=16 les années"

set ylabel "{/=16 les comptes hfr cumulés"
set key at 2000.75,575000 center left Left opaque reverse samplen 3 box spacing 1.3 width -8 height .2
set mytics 2
set ytics 100000
set size 1,0.625
set origin 0,0.3
plot [2000:2019][0:700000] \
"hfr_2019_dt_dates_active_accounts.txt" u 1:4 w lines lw 2 lc rgbcolor "#003fbf" title "{/:Bold années d'arrivée sur le forum avec au moins un post", \
"hfr_2019_dt_dates_active_accounts.txt" every ::::18 u 1:5 w lines lw 2 lc rgbcolor "#bf3f3f" title "{/:Bold années du dernier message jusqu'à 2018 inclus", \
"hfr_2019_dt_dates_active_accounts.txt" skip 18 u 1:5 w lines lw 2 lc rgbcolor "#bf3f3f" dt (2,2) notitle, \
"hfr_2019_dt_dates_active_accounts.txt" u 1:4:5 w filledcurves lw 2 lc rgbcolor "#7f3fff" title "{/:Bold les conptes hfr actifs"

minus(s) = sprintf("{/:Bold=12 %.f}", s)

set ylabel "{/=16 les comptes hfr actifs"
set key at 2018.5,75000 center right Left opaque reverse samplen 3 box spacing 1.3 width -4.5 height .2
set mytics 5
set ytics 50000
set size 1,0.3
set origin 0,0
plot [2000:2019][0:100000] \
"hfr_2019_dt_dates_active_accounts.txt" every ::::18 u 1:6 w lines lw 2 lc rgbcolor "#7f3fff" title "{/:Bold les conptes hfr actifs", \
"hfr_2019_dt_dates_active_accounts.txt" skip 18 u 1:6 w lines lw 2 lc rgbcolor "#7f3fff" dt (2,2) notitle, \
"hfr_2019_dt_dates_active_accounts.txt" u 1:6:(minus($6)) w labels center tc rgbcolor "#7f3fff" point lc rgbcolor "#df3fdf" pt 7 ps .75 offset first -.075,8000 notitle

unset multiplot
set output


