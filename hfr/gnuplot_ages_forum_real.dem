# nettoyage des données

#sed -i "s/\"//g" data_ages_forum_real_*.txt
#sed -i "s/,/ /g" data_ages_forum_real_*.txt
#sed -i "1d" data_ages_forum_real_*.txt
#del sed*


# le graph

unset multiplot
reset session
reset
set encoding utf8
set terminal svg size 1600,900 dynamic name "hfr_ages_forum_real" font "Verdana,Arial,Sans-serif,Helvetica,14" background "#ffffff"
set output "hfr_ages_forum_real.svg"
set multiplot layout 2,1 title "{/:Bold=18 hfr : l'ages des forumeurs actifs en 2019\n{/=12 (données collectées entre le 21 mars 2019 à 02:55:05 et le 26 mars 2019 à 20:54:41)"
set grid xtics mxtics ytics mytics back lc "#3f3f3f" dt (10,10), lc "#7f7f7f" dt (10,10)
set key tc variable
set style fill transparent solid 0.4
set mytics 2
set format y "%4.f"

boldus(s) = sprintf("{/:Bold %.f}", s)
minus(s) = sprintf("{/:Bold=12 %.f}", s)

set xtics 0,1,19 offset 0,0.3
set ytics 1000
set xlabel "{/=16 l'age forumesque" offset 0,0.4
set ylabel "{/=16 les comptes hfr"
set key at 18.5,3500 center right Left opaque reverse samplen 3 box spacing 1.3 width -5.5 height .2
plot [-1:20][0:4500] "data_ages_forum_real_1.txt" u 1:2 w boxes lw 2 lc rgbcolor "#ff7f00" title "{/:Bold age forumesque des forumeurs actifs en 2019", \
"data_ages_forum_real_1.txt" u 1:($2 + 250):(boldus($2)) w labels center tc rgbcolor "#ff7f00" notitle

set xtics 0,10,110 offset 0,0.3
set ytics 100
set mxtics 5
set xlabel "{/=16 l'age renseigné" offset 0,0.4
set ylabel "{/=16 les comptes hfr"
set key at 103,400 center right Left opaque reverse samplen 3 box spacing 1.3 width -6 height .2
plot [-1:111][0:500] "data_ages_forum_real_2.txt" u 1:2 w lines lw 2 lc rgbcolor "#3fbf3f" title "{/:Bold age renseigné des forumeurs actifs en 2019", \
"data_ages_forum_real_2.txt" u 1:2 w filledcurves x1 lw 2 lc rgbcolor "#3fbf3f" notitle #, \
#"data_ages_forum_real_2.txt" u 1:($2 + 25):(minus($2)) w labels left rotate tc rgbcolor "#3fbf3f" notitle

unset multiplot
set output


