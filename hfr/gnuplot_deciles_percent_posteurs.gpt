# nettoyage des données

#sed -i "s/\"//g" data_deciles_percent_posteurs_*.txt
#sed -i "s/,/ /g" data_deciles_percent_posteurs_*.txt
#sed -i "1d" data_deciles_percent_posteurs_*.txt
#del sed*


# le graph

unset multiplot
reset session
reset
set encoding utf8
set terminal svg size 1600,900 dynamic name "hfr_deciles_percent_posteurs_svg" font "Verdana,Arial,Sans-serif,Helvetica,14" background "#ffffff"
set output "hfr_deciles_percent_posteurs.svg"
set multiplot layout 1,2 title "{/:Bold=18 hfr : la répartition en déciles du top 1% des posteurs\n{/=12 (données collectées entre le 5 avril 2019 à 19:42:51 et le 12 avril 2019 à 2:48:34)"
set xtics offset 0,0.3 0.1
set x2tics offset 0,-0.3
set format y "%.f"
set style fill transparent solid 0.4
set mytics 4
set grid xtics mxtics ytics mytics back lc "#3f3f3f" dt (10,10), lc "#7f7f7f" dt (10,10)
set xlabel "{/=16 les comptes hfr (en %)" offset 0,0.4
set x2label "{/=16 le nombre de posts pour passer d'un décile à l'autre" offset 0,-0.4
set ylabel "{/=16 la somme des posts par décile"
set tmargin 4.5
set style line 1 lw 2 lc rgbcolor "#2f8f4f"
set key tc variable

boldus(s) = sprintf("{/:Bold=14 %.2f%%}", s)

set key at 99.05,56250000 top left Left opaque reverse samplen -1 box spacing 1.3 width -2 height .2
set x2tics ("1818" 99, "2123" 99.1, "2507" 99.2, "3005" 99.3, "3730" 99.4, "4712" 99.5, "6221" 99.6, "8538" 99.7, "12491" 99.8, "22413" 99.9, "207555" 100)
plot [98.95:100.05][0:60000000] "data_deciles_percent_posteurs_1.txt" u 1:2 w boxes lw 2 lc rgbcolor "#7f00ff" title "{/:Bold pour tous les comptes hfr", \
"data_deciles_percent_posteurs_1.txt" u 1:($2 + 1000000):(boldus($3)) w labels left rotate tc ls 1 title "{/:Bold % du nombre total de posts"

set key at 99.05,56250000 top left Left opaque reverse samplen -1 box spacing 1.3 width -4 height 0.2
set x2tics ("3913" 99, "4497" 99.1, "5224" 99.2, "6143" 99.3, "7289" 99.4, "8877" 99.5, "10942" 99.6, "14179" 99.7, "20055" 99.8, "32032" 99.9, "207555" 100)
plot [98.95:100.05][0:60000000] "data_deciles_percent_posteurs_2.txt" u 1:2 w boxes lw 2 lc rgbcolor "#ff007f" title "{/:Bold pour les comptes avec au moins un post", \
"data_deciles_percent_posteurs_2.txt" u 1:($2 + 1000000):(boldus($3)) w labels left rotate tc ls 1 title "{/:Bold % du nombre total de posts}"

unset multiplot
set output


