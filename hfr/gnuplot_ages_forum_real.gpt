# nettoyage des données

#sed -i "s/\"//g" data_ages_forum_real_*.txt
#sed -i "s/,/ /g" data_ages_forum_real_*.txt
#sed -i "1d" data_ages_forum_real_*.txt
#del sed*


# le graph

unset multiplot
reset session
reset
set encoding utf8
set terminal svg size 1600,900 dynamic name "hfr_ages_forum_real" font "Verdana,Arial,Sans-serif,Helvetica,14" background "#ffffff"
set output "hfr_ages_forum_real.svg"
set multiplot layout 2,1 title "{/:Bold=18 hfr : l'ages des forumeurs actifs en 2019\n{/=12 (données collectées entre le 21 mars 2019 à 02:55:05 et le 26 mars 2019 à 20:54:41)"
set grid xtics mxtics ytics mytics back lc "#3f3f3f" dt (10,10), lc "#7f7f7f" dt (10,10)
set key tc variable
set style fill transparent solid 0.4
set mytics 2
set format y "%4.f"

boldus(s) = sprintf("{/:Bold %.f}", s)
minus(s) = sprintf("{/:Bold=12 %.f}", s)


# plot 1

set xtics 0,1,19 offset 0,0.3
set ytics 1000
set xlabel "{/=16 l'age forumesque" offset 0,0.4
set ylabel "{/=16 les comptes hfr"
set key at 18.5,3500 center right Left opaque reverse samplen 3 box spacing 1.3 width -6 height .2

set arrow 1 from    8,0 to    8,4500 nohead front lw 2 lc "#ff007f" dt (10,7)
set arrow 2 from 7.79,0 to 7.79,4500 nohead front lw 2 lc "#7f00ff" dt (10,7)

set label 1 "{/:Bold 8"    at    8,2750 center rotate front tc rgbcolor "#ff007f" offset 1,0
set label 2 "{/:Bold 7.79" at 7.79,2750 center rotate front tc rgbcolor "#7f00ff" offset -1,0

plot [-1:20][0:4500] \
"data_ages_forum_real_1.txt" u 1:2 w boxes lw 2 lc rgbcolor "#ff7f00" title "{/:Bold age forumesque des forumeurs actifs en 2019", \
"data_ages_forum_real_1.txt" u 1:($2 + 250):(boldus($2)) w labels center tc rgbcolor "#ff7f00" notitle, \
keyentry w lines lw 2 lc rgbcolor "#7f00ff" dt (10,7) title "{/:Bold age forumesque moyen des forumeurs actifs en 2019", \
keyentry w lines lw 2 lc rgbcolor "#ff007f" dt (10,7) title "{/:Bold age forumesque médian des forumeurs actifs en 2019"


# plot 2

set xtics 0,10,110 offset 0,0.3
set ytics 100
set mxtics 4
set xlabel "{/=16 l'age renseigné" offset 0,0.4
set ylabel "{/=16 les comptes hfr"
set key at 103.75,400 center right Left opaque reverse samplen 3 box spacing 1.3 width -6.5 height .2

set arrow 1 from    34,0 to    34,500 nohead front lw 2 lc "#ff007f" dt (10,7)
set arrow 2 from 35.39,0 to 35.39,500 nohead front lw 2 lc "#7f00ff" dt (10,7)

set label 1 "{/:Bold 34"    at    34,175 center rotate front tc rgbcolor "#ff007f" offset -1,0
set label 2 "{/:Bold 35.39" at 35.39,175 center rotate front tc rgbcolor "#7f00ff" offset 1,0

plot [-1:111][0:500] \
"data_ages_forum_real_2.txt" u 1:2 w lines lw 2 lc rgbcolor "#3fbf3f" notitle, \
"data_ages_forum_real_2.txt" u 1:2 w filledcurves x1 lw 2 lc rgbcolor "#3fbf3f" title "{/:Bold age renseigné des forumeurs actifs en 2019", \
keyentry w lines lw 2 lc rgbcolor "#ff007f" dt (10,7) title "{/:Bold age renseigné médian des forumeurs actifs en 2019", \
keyentry w lines lw 2 lc rgbcolor "#7f00ff" dt (10,7) title "{/:Bold age renseigné moyen des forumeurs actifs en 2019" #, \
#"data_ages_forum_real_2.txt" u 1:($2 + 25):(minus($2)) w labels left rotate tc rgbcolor "#3fbf3f" notitle


# plot 3

unset arrow
unset label
unset grid
unset tics
unset xlabel
unset ylabel

set size 0.26,0.26
set origin 0.6625,0.07

undefine $data
$data << EOD
-0.05 0 1 80.6867835295327199 -80.6867835295327199 10733 55.17 # renseigné
0.05 0 1 -80.6867835295327199 80.6867835295327199 8720 44.83 # non renseigné
EOD

boldus(s) = sprintf("{/:Bold=14 %.2f%%}", s)

set object 1 rect center 0,0 size graph 1,graph 1 behind noclip fc "#ffffff" fs solid noborder

set label 1 "{/:Bold=16 age renseigné\n{/:Bold=16 et acceptable" at -1.5,0 center rotate by 90 front tc rgbcolor "#3fbf3f" offset -1.5,0
set label 2 "{/:Bold=16 age non renseigné\n{/:Bold=16 ou trop foireux" at 1.5,0 center rotate by -90 front tc rgbcolor "#7f7f7f" offset 1.5,0

plot [-2:2][-2:2] \
$data every ::::0 u 1:2:3:4:5 w circles fc rgbcolor "#3fbf3f" lw 2 title "age renseigné et acceptable", \
$data skip 1 u 1:2:3:4:5 w circles fc rgbcolor "#7f7f7f" lw 2 title "age non renseigné ou trop foireux", \
$data every ::::0 u ($1 - 0.5):2:(boldus($7)) w labels tc rgbcolor "#3fbf3f" notitle, \
$data skip 1 u ($1 + 0.5):2:(boldus($7)) w labels tc rgbcolor "#7f7f7f" notitle

unset multiplot
set output


