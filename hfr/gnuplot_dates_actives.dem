# nettoyage des données

#sed -i "s/\"//g" data_actives.txt
#sed -i "s/,/ /g" data_actives.txt
#sed -i "1d" data_actives.txt
#del sed*


# le graph

reset
set terminal svg size 1600,900 dynamic name "hfr_dates_actives" font "Verdana,Arial,Sans-serif,Helvetica,14" background "#ffffff"
set output "hfr_dates_actives.svg"
set multiplot title "{/:Bold=18 hfr : les comptes hfr actifs\n{/=12 (données collectées entre le 21 mars 2019 à 02:55:05 et le 26 mars 2019 à 20:54:41)"
set style fill transparent solid 0.4
set key tc variable
set grid xtics ytics mxtics mytics back lc "#3f3f3f" dt (10,10), lc "#7f7f7f" dt (10,10)
set xtics 1
set mxtics 2
set format y "%7.f"
set xlabel "{/=16 les années"

set ylabel "{/=16 les comptes hfr cumulés"
set key at 2000.75,575000 center left Left opaque reverse samplen 3 box spacing 1.3 width -8 height .2
set mytics 2
set ytics 100000
set size 1,0.625
set origin 0,0.3
plot [2000:2019][0:700000] \
"data_dates_actives.txt" u 1:4 w lines lw 2 lc rgbcolor "#003fbf" title "{/:Bold années d'arrivée sur le forum avec au moins un post", \
"data_dates_actives.txt" u 1:5 w lines lw 2 lc rgbcolor "#bf3f3f" title "{/:Bold années du dernier message avant 2019", \
"data_dates_actives.txt" u 1:4:5 w filledcurves lw 2 lc rgbcolor "#bf3fff" title "{/:Bold les conptes hfr actifs"

set ylabel "{/=16 les comptes hfr actifs"
set key at 2018.25,75000 center right Left opaque reverse samplen 3 box spacing 1.3 width -4 height .2
set mytics 5
set ytics 50000
set size 1,0.3
set origin 0,0
plot [2000:2019][0:100000] \
"data_dates_actives.txt" u 1:6 w lines lw 2lc rgbcolor "#bf3fff" title "{/:Bold les conptes hfr actifs"

unset multiplot
set output


